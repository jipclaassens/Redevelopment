container AdditionalOperations
{
	unit<uint32> Nieuwbouw := PrepDomains/N/domain/zonder_afwijkende_pand_geometrieen
	{
		unit<uint32>          intersect_met_SN     := bg_overlay_polygon(pand_geometry, SN/Result/geometry_rd);
		attribute<bool>       HeeftOverlapmet_SN   := IsDefined(rlookup(id(.),intersect_met_SN/first_rel));
		
		//Als nieuwbouw al is geindetificeerd als SN wil je die hier niet ook meenemen
		unit<uint32>  Result   := select_with_org_rel(NOT(HeeftOverlapmet_SN))
		{
			attribute<rdc>    pand_geometry (poly) := ../pand_geometry[org_rel];
			attribute<rdc>    vbo_geometry         := ../vbo_geometry[org_rel];
			attribute<int32>  MutatieMaand         := ../MutatieMaand[org_rel];
			attribute<int32>  vbo_oppervlakte      := ../vbo_oppervlakte[org_rel];
			attribute<jaar>   pand_bouwjaar        := ../pand_bouwjaar[org_rel];
			attribute<uint64> vbo_bag_nr           := ../vbo_bag_nr[org_rel];
		}
	}
	unit<uint32> Sloop := union_unit(PrepDomains/S/domain, PrepDomains/S_nw/domain)
	{
		attribute<rdc>     pand_geometry    (poly) := union_data(., PrepDomains/S/domain/pand_geometry, PrepDomains/S_nw/domain/pand_geometry);
		attribute<uint64>  pand_bag_nr             := union_data(., PrepDomains/S/domain/pand_bag_nr, PrepDomains/S_nw/domain/pand_bag_nr);
		attribute<rdc>     vbo_geometry            := union_data(., PrepDomains/S/domain/vbo_geometry, PrepDomains/S_nw/domain/vbo_geometry);
		attribute<int32>   vbo_oppervlakte         := union_data(., PrepDomains/S/domain/vbo_oppervlakte, PrepDomains/S_nw/domain/vbo_oppervlakte);
		attribute<jaar>    pand_bouwjaar           := union_data(., PrepDomains/S/domain/pand_bouwjaar, PrepDomains/S_nw/domain/pand_bouwjaar);
		attribute<uint64>  vbo_bag_nr              := union_data(., PrepDomains/S/domain/vbo_bag_nr, PrepDomains/S_nw/domain/vbo_bag_nr);
		attribute<int32>   MutatieMaand            := union_data(., PrepDomains/S/domain/MutatieMaand, PrepDomains/S_nw/domain/MutatieMaand);
		
		attribute<bool>    IsWoon                  := union_data(., const(TRUE, PrepDomains/S/domain), const(FALSE, PrepDomains/S_nw/domain));

		unit<uint32>          intersect_met_SN     := bg_overlay_polygon(pand_geometry, SN/Result/geometry_rd);
		attribute<bool>       HeeftOverlapmet_SN   := IsDefined(rlookup(id(.),intersect_met_SN/first_rel));
		
		//Als sloop al is geindetificeerd als SN wil je die hier niet ook meenemen
		unit<uint32>  Result   := select_with_org_rel(NOT(HeeftOverlapmet_SN) && IsWoon)
		{
			attribute<rdc>    pand_geometry (poly) := ../pand_geometry[org_rel];
			attribute<rdc>    vbo_geometry         := ../vbo_geometry[org_rel];
			attribute<int32>  MutatieMaand         := ../MutatieMaand[org_rel];
			attribute<int32>  vbo_oppervlakte      := ../vbo_oppervlakte[org_rel];
			attribute<jaar>   pand_bouwjaar        := ../pand_bouwjaar[org_rel];
			attribute<uint64> vbo_bag_nr           := ../vbo_bag_nr[org_rel];
			
			//adhoc
			attribute<bool>   ZitVBOookalinSNSloop   := IsDefined(rlookup(vbo_bag_nr, AdditionalOperations/SN/Result/Sloop_Objecten/vbo_bag_nr));
		}
	}
	
	container SN 
	{
		//ik wil SN identificeren. Door nieuwbouw panden die op hetzelfde perceel zijn gebouwd als er panden gesloopt zijn. 
		//Dus we gaan uit van de nieuwere perceelsgrenzen, die waarschijnlijk corresponderen met de nieuwbouw. 
		//Daarnaast wil ik graag ruimtelijk clusteren. Gesloopte panden die naast elkaar staan zijn onderdeel van hetzelfde SN-project. Dus Sloop pand (wonen of niet wonen) inflaten, dissolven, deflaten.
		//Nieuwbouw percelen (perceel dat NB panden bevat) clusteren: inflate, dissolve, deflate.
		//Sloop percelen (perceel dat sloop panden bevat) clusteren: inflate, dissolve, deflate.
				
		// sloop panden + buffer - wegen
		// nieuwbouw panden + buffer - wegen
		
		// clean up, want bufferdelen die afgesneden zijn door wegen 
			// door overlay met originele panden, en relaties
			
		// dissolve van gecleande NB en Sloop buffers. 
		
		//samenvoegen van sloop en nieuwbouw sites
		//check of sloop en nieuwbouw plaatsvindt, en of het aandeel voldoende is
	
		unit<uint32> SloopPanden := unique(Sloop/pand_bag_nr)
		{
			attribute<rdc>     geometry                  (poly) := Sloop/pand_geometry[Sloop_rel];
			attribute<uint64>  pand_bag_nr                      := values;
			attribute<Sloop>   Sloop_rel                        := rlookup(pand_bag_nr, Sloop/pand_bag_nr);
			attribute<m2>      area_pand                        := area(geometry, m2);
			
			//buffer om de panden heen zodat deze later gerelateerd kunnen worden aan de nieuwbouw panden.
			attribute<rdc> inflate                       (poly) := bg_buffer_multi_polygon(geometry,  parameters/big_inflate_m[float64], 16b);
			
			//Maar, deze buffers mogen niet over een weg heen gaan. Dus die snijden we eruit.
			unit<uint32> PandenBuffer_x_WegSpoor                := bg_overlay_polygon(inflate, SourceData/BRT/Read_Relevant_Tiles_x_WegSpoor/TiledNonWegSpoor);
			unit<uint32> PandenBuffer_x_WegSpoor_Split          := bp_split_union_polygon(PandenBuffer_x_WegSpoor/geometry[rdc_cm])
			{
				attribute<rdc> geometry_rd               (poly) := geometry[rdc];
			}
			//De bufferdelen die door een weg zijn afgesneden, moeten eruit worden gefilterd.
			unit<uint32> PandenBuffer_Minus_WegSpoor_x_Panden   := bg_overlay_polygon(PandenBuffer_x_WegSpoor_Split/geometry_rd, geometry)
			{
				//dit zijn dan de panden buffers, zonder de door de wegafgesneden stukken.
				attribute<rdc> Cleaned                   (poly) := PandenBuffer_x_WegSpoor_Split/geometry_rd[first_rel];
			}
		}
		
		unit<uint32> NieuwbouwPanden := unique(Nieuwbouw/pand_bag_nr)
		{
			attribute<rdc>                      geometry              (poly) := Nieuwbouw/pand_geometry[Nieuwbouw_rel];
			attribute<uint64>                   pand_bag_nr                  := values;
			attribute<Nieuwbouw>                Nieuwbouw_rel                := rlookup(pand_bag_nr, Nieuwbouw/pand_bag_nr);
			attribute<m2>                       area_pand                    := area(geometry, m2);
			
			//buffer om de panden heen zodat deze later gerelateerd kunnen worden aan de sloop panden.
			attribute<rdc> inflate                       (poly) := bg_buffer_multi_polygon(geometry,  parameters/big_inflate_m[float64], 16b);
			
			//Maar, deze buffers mogen niet over een weg heen gaan. Dus die snijden we eruit.
			unit<uint32> PandenBuffer_x_WegSpoor                := bg_overlay_polygon(inflate, SourceData/BRT/Read_Relevant_Tiles_x_WegSpoor/TiledNonWegSpoor);
			unit<uint32> PandenBuffer_x_WegSpoor_Split          := bp_split_union_polygon(PandenBuffer_x_WegSpoor/geometry[rdc_cm])
			{
				attribute<rdc> geometry_rd               (poly) := geometry[rdc];
			}
			
			//De bufferdelen die door een weg zijn afgesneden, moeten eruit worden gefilterd.
			unit<uint32> PandenBuffer_Minus_WegSpoor_x_Panden   := bg_overlay_polygon(PandenBuffer_x_WegSpoor_Split/geometry_rd, geometry)
			{
				//dit zijn dan de panden buffers, zonder de door de wegafgesneden stukken.
				attribute<rdc> Cleaned                   (poly) := PandenBuffer_x_WegSpoor_Split/geometry_rd[first_rel];
			}
		}
		
		unit<uint32>  BestaandWoongebiedIn2000   := select_with_org_rel(/SourceData/bestand_bodem_gebruik/BBG/BBG_Reeks_1996_2017/IsWoongebied_2000)
		{
			attribute<rdc> geometry (poly) := org_rel -> geometry;
			attribute<m2>  area_vlak       := area(geometry, m2);
		}
	
		// Wat als nieuwbouw panden op een locatie staan waar gesloopt is voordat dit in de bag opgenomen werd? Dan missen we deze SN, en wordt dit onterecht als 'slechts' nieuwbouw beschouwd. 
		// Daarom confronteren we de nieuwbouw panden met bbg woongebied uit 2000. Als het toen al wonen was, is het waarschijnlijk dat er dus iets stond en dus SN is.
		unit<uint32>   BBGWoon_x_NieuwbouwPanden            := bg_overlay_polygon(NieuwbouwPanden/geometry, BestaandWoongebiedIn2000/geometry)
		, DialogData = "pand_geometry", DialogType = "map"
		{
			attribute<m2>      area_pand                    := NieuwbouwPanden/area_pand[first_rel];
			attribute<rdc>     pand_geometry (poly)         := NieuwbouwPanden/geometry[first_rel];
			
			//buffer om de panden heen zodat deze later gerelateerd kunnen worden aan de sloop panden.
			attribute<rdc>     inflate               (poly) := bg_buffer_multi_polygon(pand_geometry,  parameters/big_inflate_m[float64], 16b);
			
			//Maar, deze buffers mogen niet over een weg heen gaan. Dus die snijden we eruit.
			unit<uint32> PandenBuffer_x_WegSpoor                := bg_overlay_polygon(inflate, SourceData/BRT/Read_Relevant_Tiles_x_WegSpoor/TiledNonWegSpoor);
			unit<uint32> PandenBuffer_x_WegSpoor_Split          := bp_split_union_polygon(PandenBuffer_x_WegSpoor/geometry[rdc_cm])
			{
				attribute<rdc> geometry_rd               (poly) := geometry[rdc];
			}
			
			//De bufferdelen die door een weg zijn afgesneden, moeten eruit worden gefilterd.
			unit<uint32> PandenBuffer_Minus_WegSpoor_x_Panden   := bg_overlay_polygon(PandenBuffer_x_WegSpoor_Split/geometry_rd, geometry)
			{
				//dit zijn dan de panden buffers, zonder de door de wegafgesneden stukken.
				attribute<rdc> Cleaned                   (poly) := PandenBuffer_x_WegSpoor_Split/geometry_rd[first_rel];
			}
		}
		
		// merge de sloop en nb gebufferde panden, zonder de afgesneden stukken.
		unit<uint32> Merge_NB_Sloop_Panden_Buffer := union_unit(SloopPanden/PandenBuffer_Minus_WegSpoor_x_Panden, NieuwbouwPanden/PandenBuffer_Minus_WegSpoor_x_Panden, BBGWoon_x_NieuwbouwPanden/PandenBuffer_Minus_WegSpoor_x_Panden)
		{
			attribute<rdc> geometry                      (poly) := union_data(., SloopPanden/PandenBuffer_Minus_WegSpoor_x_Panden/cleaned, NieuwbouwPanden/PandenBuffer_Minus_WegSpoor_x_Panden/cleaned, BBGWoon_x_NieuwbouwPanden/PandenBuffer_Minus_WegSpoor_x_Panden/cleaned);
			
			//opschonen van de geometry
			unit<uint32>   Dissolve                             := bp_split_union_polygon(geometry[rdc_cm]), DialogData = "Deflate", DialogType = "map";
			attribute<rdc> Outer               (poly, dissolve) := bg_outer_multi_polygon(Dissolve/geometry[rdc]);
			attribute<rdc> Inflate             (poly, dissolve) := bg_buffer_multi_polygon(Outer, parameters/big_inflate_m[float64], 16b);
			attribute<rdc> Deflate             (poly, dissolve) := bg_buffer_multi_polygon(Inflate, -parameters/big_inflate_m[float64], 16b);
			
			
			//nu willen we alleen die polygon overhouden die sloop EN nieuwbouw panden bevatten.
			unit<uint32>   x_SloopPanden                        := bg_overlay_polygon(SloopPanden/geometry, Deflate)
			{
				attribute<m2>  area_pand                        := SloopPanden/area_pand[first_rel];
			}
			unit<uint32>   x_NieuwbouwPanden                    := bg_overlay_polygon(NieuwbouwPanden/geometry, Deflate)
			{
				attribute<m2>  area_pand                        := NieuwbouwPanden/area_pand[first_rel];
			}
			
			unit<uint32>   x_NBpanden_BBGwoongebied             := bg_overlay_polygon(BBGWoon_x_NieuwbouwPanden/pand_geometry, Deflate)
			{
				attribute<m2> area_pand                         := BBGWoon_x_NieuwbouwPanden/area_pand[first_rel];
			}
			
			attribute<m2>                Sloop_Pand_Area            (dissolve) := sum(x_SloopPanden/area_pand, x_SloopPanden/second_rel);
			attribute<m2>                Nieuwbouw_Pand_Area        (dissolve) := sum(x_NieuwbouwPanden/area_pand, x_NieuwbouwPanden/second_rel);
			attribute<m2>                NBpanden_BBGwoon_Pand_Area (dissolve) := sum(x_NBpanden_BBGwoongebied/area_pand, x_NBpanden_BBGwoongebied/second_rel);
			attribute<bool>              HasSloopPanden             (dissolve) := Sloop_Pand_Area > 0[m2];
			attribute<bool>              HasNieuwbouwPanden         (dissolve) := Nieuwbouw_Pand_Area > 0[m2];
			attribute<bool>              HasNBpanden_BBGwoon_Panden (dissolve) := NBpanden_BBGwoon_Pand_Area > 0[m2];
			
			attribute<m2>                Site_Area                  (dissolve) := area(Deflate, m2);
			attribute<float32>           Share_S_pand_op_site       (dissolve) := Sloop_Pand_Area / Site_Area;
			attribute<float32>           Share_N_pand_op_site       (dissolve) := Nieuwbouw_Pand_Area / Site_Area;
			attribute<float32>           Share_NB_BBG_pand_op_site  (dissolve) := NBpanden_BBGwoon_Pand_Area / Site_Area;
			
			attribute<bool>              HasLargeEnoughSN_shares    (dissolve) := Share_S_pand_op_site > Parameters/minimum_SN_area_share && Share_N_pand_op_site > Parameters/minimum_SN_area_share;
			attribute<bool>              HasLargeEnoughSN_BBG_share (dissolve) := Share_NB_BBG_pand_op_site > Parameters/minimum_SN_area_share;
		}
		
		// neem alleen de merge over waar sloop EN nieuwbouw op plaatsvindt
		unit<uint32> Sites_met_SloopENnieuwbouw := 	select_with_org_rel(
														   (Merge_NB_Sloop_Panden_Buffer/HasSloopPanden 
																&& Merge_NB_Sloop_Panden_Buffer/HasNieuwbouwPanden  
																&& Merge_NB_Sloop_Panden_Buffer/HasLargeEnoughSN_shares)
																|| (Merge_NB_Sloop_Panden_Buffer/HasNBpanden_BBGwoon_Panden 
																	&& Merge_NB_Sloop_Panden_Buffer/HasLargeEnoughSN_BBG_share)
													)
		{
			attribute<rdc> geometry (poly)           := Merge_NB_Sloop_Panden_Buffer/Deflate[org_rel];
		}
		
		//Het kan zijn dat delen van de oorspronkelijke nieuwbouw en sloop geometries zijn weggevallen, die moeten dus terug.
		//Dus zoeken we naar overlap in de vorige resultaat set.
		unit<uint32>   x_SloopPanden                        := bg_overlay_polygon(SloopPanden/geometry, Sites_met_SloopENnieuwbouw/geometry)
		{
			attribute<rdc>  pand_geometry (poly) := SloopPanden/geometry[first_rel];
		}
		unit<uint32>   x_NieuwbouwPanden                    := bg_overlay_polygon(NieuwbouwPanden/geometry, Sites_met_SloopENnieuwbouw/geometry)
		{
			attribute<rdc>  pand_geometry (poly) := NieuwbouwPanden/geometry[first_rel];
		}
	
		unit<uint32> Union_with_original_geometry := union_unit(Sites_met_SloopENnieuwbouw, x_SloopPanden, x_NieuwbouwPanden)
		{
			attribute<rdc> geometry                  (poly) := union_data(.,Sites_met_SloopENnieuwbouw/geometry, x_SloopPanden/pand_geometry, x_NieuwbouwPanden/pand_geometry);
		}
		
		unit<uint32> Result                       := bp_split_union_polygon(Union_with_original_geometry/geometry[rdc_cm])
		{
			attribute<rdc> geometry_rd               (poly) := geometry[rdc];
			
			unit<uint32>             Sloop                              := PrepDomains/S/domain;
			unit<uint32>             Sloop_NW                           := PrepDomains/S_NW/domain;
			unit<uint32>             Nieuwbouw                          := PrepDomains/N/domain;
			attribute<string>        Site_ID                            := 'SN_'+string(id(.));
			attribute<.>             Sloop_rel     (Sloop)              := point_in_polygon(Sloop/vbo_geometry, geometry_rd);
			attribute<.>             Sloop_NW_rel  (Sloop_NW)           := point_in_polygon(Sloop_NW/vbo_geometry, geometry_rd);
			attribute<.>             Nieuwbouw_rel (Nieuwbouw)          := point_in_polygon(Nieuwbouw/vbo_geometry, geometry_rd);
			
			attribute<bool>          IsSN_door_BBG                      := IsNull(invert(Sloop_rel)) && IsNull(invert(Sloop_NW_rel)) && IsDefined(invert(Nieuwbouw_rel));
			
			attribute<m2>            area                               := area(geometry_rd, m2);
			attribute<jaar>          Avg_bouwjaar                       := mean(Sloop/pand_bouwjaar, Sloop_rel);
			attribute<jaar>          SD_bouwjaar                        := sd(Sloop/pand_bouwjaar, Sloop_rel);
			attribute<int32>         First_Nieuwbouw_MutatieMaand       := min(IsDefined(Nieuwbouw/MutatieMaand) ? Nieuwbouw/MutatieMaand : 99999999i, Nieuwbouw_rel);
			attribute<int32>         First_Sloop_MutatieMaand           := min(IsDefined(Sloop/MutatieMaand)     ? Sloop/MutatieMaand     : 99999999i, Sloop_rel);
			attribute<int32>         Last_Nieuwbouw_MutatieMaand        := max(IsDefined(Nieuwbouw/MutatieMaand) ? Nieuwbouw/MutatieMaand : 9999i, Nieuwbouw_rel);
			attribute<int32>         Last_Sloop_MutatieMaand            := max(IsDefined(Sloop/MutatieMaand)     ? Sloop/MutatieMaand     : 9999i, Sloop_rel);
			
			attribute<int32>         MutatieMaand                       := First_Sloop_MutatieMaand > 21000000i 
																				? Last_Nieuwbouw_MutatieMaand < 0i 
																					? null_i 
																					: Last_Nieuwbouw_MutatieMaand
																				: First_Sloop_MutatieMaand;
				
			unit<uint32> Sloop_Objecten     := select_with_org_rel(IsDefined(Sloop_rel))
			{
				attribute<rdc>    pand_geometry (poly) := Sloop/pand_geometry[org_rel];
				attribute<rdc>    vbo_geometry         := Sloop/vbo_geometry[org_rel];
				attribute<int32>  MutatieMaand         := Sloop/MutatieMaand[org_rel];
				attribute<int32>  vbo_oppervlakte      := Sloop/vbo_oppervlakte[org_rel];
				attribute<jaar>   pand_bouwjaar        := Sloop/pand_bouwjaar[org_rel];
				attribute<string> Site_ID              := ../Site_ID[../sloop_rel][org_rel];
				attribute<uint64> vbo_bag_nr           := Sloop/vbo_bag_nr[org_rel];
				
				//adhoc
				attribute<bool>   ZitVBOookalinSloop   := IsDefined(rlookup(vbo_bag_nr, AdditionalOperations/Sloop/Result/vbo_bag_nr));
				
			}
			unit<uint32> Sloop_NW_Objecten     := select_with_org_rel(IsDefined(Sloop_NW_rel))
			{
				attribute<rdc>    pand_geometry (poly) := Sloop_NW/pand_geometry[org_rel];
				attribute<rdc>    vbo_geometry         := Sloop_NW/vbo_geometry[org_rel];
				attribute<int32>  MutatieMaand         := Sloop_NW/MutatieMaand[org_rel];
				attribute<int32>  vbo_oppervlakte      := Sloop_NW/vbo_oppervlakte[org_rel];
				attribute<jaar>   pand_bouwjaar        := Sloop_NW/pand_bouwjaar[org_rel];
				attribute<string> Site_ID              := ../Site_ID[../Sloop_NW_rel][org_rel];
				attribute<uint64> vbo_bag_nr           := Sloop_NW/vbo_bag_nr[org_rel];
			}
			unit<uint32> Nieuwbouw_Objecten := select_with_org_rel(IsDefined(Nieuwbouw_rel))
			{
				attribute<rdc>    pand_geometry (poly) := Nieuwbouw/pand_geometry[org_rel];
				attribute<rdc>    vbo_geometry         := Nieuwbouw/vbo_geometry[org_rel];
				attribute<int32>  MutatieMaand         := Nieuwbouw/MutatieMaand[org_rel];
				attribute<int32>  vbo_oppervlakte      := Nieuwbouw/vbo_oppervlakte[org_rel];
				attribute<jaar>   pand_bouwjaar        := Nieuwbouw/pand_bouwjaar[org_rel];
				attribute<string> Site_ID              := ../Site_ID[../Nieuwbouw_rel][org_rel];
				attribute<uint64> vbo_bag_nr           := Nieuwbouw/vbo_bag_nr[org_rel];
			}
		}
	}
}
