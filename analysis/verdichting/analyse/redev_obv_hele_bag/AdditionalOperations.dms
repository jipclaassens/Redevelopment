container AdditionalOperations
{
	//welke panden stonden er al in 2012?
	unit<uint32>  BestaandeBouwIn2012        := PrepBAG/BeginVoorraad
	{
		attribute<rdc>            pand_geometry (poly)                := PrepBAG/BeginVoorraad/pand_geometry;
	}
	
	//Subset van percelen waarop sloop van woningen plaats vindt
	unit<uint32> Sloop_perceel := select_with_org_rel(IsDefined(invert(PrepDomains/S/domain/overlay_metPercelen/first_rel))) 
	{
		attribute<rdc>            perceel_geometry (poly)                  := org_rel -> geometry[rdc];
	}
	
	//Subset van percelen waarop nieuwbouw woningen komen
	unit<uint32> Nieuwbouw_perceel := select_with_org_rel(IsDefined(invert(PrepDomains/N/domain/overlay_metPercelen/first_rel)))
	{
		attribute<rdc>            perceel_geometry (poly)                  := org_rel -> geometry[rdc];
		
		
		// Stonden er al panden op dit perceel waar nieuwbouw woningen komen?
		attribute<bool>           IsNieuwbouw_OpBebouwd                    := IsDefined(rlookup(id(.),Nieuwbouw_OpBebouwd/second_rel)); 
		unit<uint32>              Nieuwbouw_OpBebouwd                      := overlay_polygon(BestaandeBouwIn2012/pand_geometry[rdc_mm], perceel_geometry[rdc_mm]); //is er overlap tussen dit perceel met nieuwbouw en bestaand gebouwen?

		unit<uint32>                   Nieuwbouw_src                       := PrepDomains/N/domain //nieuwbouw panden/objecten
		{
			attribute<bool>            IsNieuwbouw_OpOnbebouwd             := IsDefined(point_in_polygon(vbo_geometry, Nieuwbouw_OpOnbebouwd_perceel/perceel_geometry));
			attribute<bool>            IsNieuwbouw_OpBebouwd               := IsDefined(point_in_polygon(vbo_geometry, Nieuwbouw_OpBebouwd_perceel/perceel_geometry));
		}
		unit<uint32>                   Nieuwbouw_OpOnbebouwd_perceel       := select_with_org_rel(!./IsNieuwbouw_OpBebouwd)
		{        
			attribute<rdc>  perceel_geometry (poly)                     := org_rel -> perceel_geometry;
		}
		unit<uint32>                   Nieuwbouw_OpBebouwd_perceel         := select_with_org_rel(./IsNieuwbouw_OpBebouwd)
		{        
			attribute<rdc>  perceel_geometry (poly)                     := org_rel -> perceel_geometry;
		}
	}
	
	// afgevallen percelen uit SN analyse omdat ze te klein zijn? of fractie S te klein?
	unit<uint32>  Nieuwbouw_Panden_OpOnbebouwd  := select_with_org_rel(Nieuwbouw_perceel/Nieuwbouw_src/IsNieuwbouw_OpOnbebouwd)
	{
		attribute<rdc>                   pand_geometry (poly)               := Nieuwbouw_perceel/Nieuwbouw_src/pand_geometry[org_rel];
		attribute<rdc>                   vbo_geometry                       := Nieuwbouw_perceel/Nieuwbouw_src/vbo_geometry[org_rel];
		attribute<int32>                 MutatieMaand                       := Nieuwbouw_perceel/Nieuwbouw_src/MutatieMaand[org_rel];
		attribute<int32>                 vbo_oppervlakte                    := Nieuwbouw_perceel/Nieuwbouw_src/vbo_oppervlakte[org_rel];
		attribute<jaar>                  pand_bouwjaar                      := Nieuwbouw_perceel/Nieuwbouw_src/pand_bouwjaar[org_rel];
		attribute<bool>                  GeenOverlapmet_SN                  := !IsDefined(rlookup(id(.),intersect_met_SN/first_rel));
		unit<uint32>                     intersect_met_SN                   := overlay_polygon(pand_geometry[rdc_mm], SN/Result/geometry_mm);
		
		unit<uint32>  Cluster   := select_with_attr_by_cond(., GeenOverlapmet_SN)
		{
			unit<uint32> Step1 := split_union_polygon(pand_geometry[rdc_mm])
			{
				attribute<rdc_mm> inflate (poly) := bg_buffer_polygon(geometry, parameters/big_inflate, 8b);
			}
			
			unit<uint32> Step2 := split_polygon(Step1/inflate)
			{
				attribute<rdc_mm> deflate (poly)                      := bg_buffer_polygon(geometry, -parameters/big_inflate, 8b);
				attribute<rdc>    geometry_rd (poly)                  := deflate[rdc];
				attribute<m2>     area                                := area(geometry_rd, m2);
			}
		}
		
		unit<uint32>  Result   := select_with_org_rel(Cluster/Step2/area >= Parameters/minimum_site_area)
		{
			attribute<rdc>    geometry (poly)                  := org_rel -> geometry_rd;
			attribute<string> Site_ID                          := 'NB_Onbeb_'+string(id(.));
			attribute<jaar>   Avg_bouwjaar                     := mean(../pand_bouwjaar, Objecten_rel);
			attribute<jaar>   SD_bouwjaar                      := sd(../pand_bouwjaar, Objecten_rel);
			attribute<int32>  MutatieMaand                     := min(../MutatieMaand, Objecten_rel) > 21000000i || min(../MutatieMaand, Objecten_rel) < 0i
																				? null_i 
																				: min(../MutatieMaand, Objecten_rel);
			attribute<m2>     area                             := area(geometry, m2);
			
			attribute<.>      Objecten_rel     (..)            := point_in_polygon(../vbo_geometry, geometry);
			
			unit<uint32>      Objecten                         := select_with_org_rel(IsDefined(Objecten_rel))
			{
				attribute<rdc>    pand_geometry (poly) := .../pand_geometry[org_rel];
				attribute<rdc>    vbo_geometry         := .../vbo_geometry[org_rel];
				attribute<int32>  vbo_oppervlakte      := .../vbo_oppervlakte[org_rel];
				attribute<jaar>   pand_bouwjaar        := .../pand_bouwjaar[org_rel];
				attribute<int32>  MutatieMaand         := .../MutatieMaand[org_rel];
				attribute<string> Site_ID              := 'NB_Onbeb_'+string(Objecten_rel[org_rel]);
			}
			
		}
	}
	unit<uint32>  Nieuwbouw_Panden_OpBebouwd    := select_with_org_rel(Nieuwbouw_perceel/Nieuwbouw_src/IsNieuwbouw_OpBebouwd)
	{
		attribute<rdc>                   pand_geometry (poly)               := Nieuwbouw_perceel/Nieuwbouw_src/pand_geometry[org_rel];
		attribute<rdc>                   vbo_geometry                       := Nieuwbouw_perceel/Nieuwbouw_src/vbo_geometry[org_rel];
		attribute<jaar>                  pand_bouwjaar                      := Nieuwbouw_perceel/Nieuwbouw_src/pand_bouwjaar[org_rel];
		attribute<int32>                 MutatieMaand                       := Nieuwbouw_perceel/Nieuwbouw_src/MutatieMaand[org_rel];
		attribute<int32>                 vbo_oppervlakte                    := Nieuwbouw_perceel/Nieuwbouw_src/vbo_oppervlakte[org_rel];
		attribute<bool>                  GeenOverlapmet_SN                  := !IsDefined(rlookup(id(.),intersect_met_SN/first_rel));
		unit<uint32>                     intersect_met_SN                   := overlay_polygon(pand_geometry[rdc_mm], SN/Result/geometry_mm);
		
		unit<uint32>  Cluster   := select_with_attr_by_cond(., GeenOverlapmet_SN)
		{
			unit<uint32> Step1 := split_union_polygon(pand_geometry[rdc_mm])
			{
				attribute<rdc_mm> inflate (poly) := bg_buffer_polygon(geometry, parameters/big_inflate, 8b);
			}
			
			unit<uint32> Step2 := split_polygon(Step1/inflate)
			{
				attribute<rdc_mm> deflate (poly)                      := bg_buffer_polygon(geometry, -parameters/big_inflate, 8b);
				attribute<rdc>    geometry_rd (poly)                  := deflate[rdc];
				attribute<m2>     area                                := area(geometry_rd, m2);
			}
		}
		
		unit<uint32>  Result   := select_with_org_rel(Cluster/Step2/area >= Parameters/minimum_site_area)
		{
			attribute<rdc>    geometry (poly)                  := org_rel -> geometry_rd;
			attribute<string> Site_ID                          := 'NB_Beb_'+string(id(.));
			attribute<jaar>   Avg_bouwjaar                     := mean(../pand_bouwjaar, Objecten_rel);
			attribute<jaar>   SD_bouwjaar                      := sd(../pand_bouwjaar, Objecten_rel);
			attribute<int32>  MutatieMaand                     := min(../MutatieMaand, Objecten_rel) > 21000000i || min(../MutatieMaand, Objecten_rel) < 0i
																				? null_i 
																				: min(../MutatieMaand, Objecten_rel);
			
			attribute<m2>     area                             := area(geometry, m2);
			attribute<.>      Objecten_rel     (..)            := point_in_polygon(../vbo_geometry, geometry);
			unit<uint32>      Objecten                         := select_with_org_rel(IsDefined(Objecten_rel))
			{
				attribute<rdc>    pand_geometry (poly) := .../pand_geometry[org_rel];
				attribute<rdc>    vbo_geometry         := .../vbo_geometry[org_rel];
				attribute<int32>  MutatieMaand         := .../MutatieMaand[org_rel];
				attribute<int32>  vbo_oppervlakte      := .../vbo_oppervlakte[org_rel];
				attribute<jaar>   pand_bouwjaar        := .../pand_bouwjaar[org_rel];
				attribute<string> Site_ID              := 'NB_Beb_'+string(Objecten_rel[org_rel]);
			}
		}
	}
	
	container SN 
	{
		unit<uint32> Sloop_domain := PrepDomains/S/domain
		{ 
			attribute<rdc_mm>                           pand_geometry_mm (poly)            := pand_geometry[rdc_mm];
			attribute<rdc>                              vbo_geometry                       := PrepDomains/S/domain/vbo_geometry;
			// attribute<NB_metSloop_TouchSloopPerc/Step3> Aggregated_rel      := point_in_polygon(vbo_geometry[rdc_mm], NB_metSloop_TouchSloopPerc/Step3/inflate);
			
			unit<uint32> Step1 := split_union_polygon(pand_geometry_mm)
			{
				attribute<rdc_mm>        inflate (poly)                     := bg_buffer_polygon(geometry,  parameters/big_inflate, 8b); //vind aangrenzende percelen en voeg die samen
			}
			
			unit<uint32> Step2 := split_union_polygon(Step1/inflate)
			{
				attribute<rdc_mm>        deflate (poly)                     := bg_buffer_polygon(geometry,  -parameters/big_inflate, 8b);
			}
		}
		
		// unit<uint32> Nieuwbouw_domain  := PrepDomains/N/domain
		// { 
			// attribute<rdc_mm>            pand_geometry_mm (poly)                 := pand_geometry[rdc_mm];
			
			// unit<uint32> Step1 := split_union_polygon(pand_geometry_mm)
			// {
				// attribute<rdc_mm>        inflate (poly)                     := bg_buffer_polygon(geometry,  parameters/big_inflate, 8b); //vind aangrenzende panden en voeg die samen
			// }
			
			// unit<uint32> Step2 := split_union_polygon(Step1/inflate)
			// {
				// attribute<rdc_mm>        deflate (poly)                     := bg_buffer_polygon(geometry,  -parameters/big_inflate, 8b);  //deflate 
			// }
		// }
		
		unit<uint32> NieuwbouwPerceel_Tile := Nieuwbouw_perceel
		{
			attribute<rdc_mm>            inflate (poly)                     := bg_buffer_polygon(perceel_geometry[rdc_mm],  parameters/supersmall_inflate, 8b);   
		
			unit<uint32> Step1 := split_union_polygon(inflate)
			{
				attribute<rdc_mm>        deflate (poly)                     := bg_buffer_polygon(geometry,  -parameters/big_inflate, 8b);  
			}
			
			unit<uint32> Step2 := split_union_polygon(Step1/deflate)
			{
				attribute<rdc_mm>        inflate (poly)                     := bg_buffer_polygon(geometry,  parameters/big_inflate, 8b);   
			}
		}
		
		unit<uint32> SloopPerceel_Tile := Sloop_perceel
		{
			attribute<rdc_mm>            inflate (poly)                     := bg_buffer_polygon(perceel_geometry[rdc_mm],  parameters/supersmall_inflate, 8b);   
			
			unit<uint32> Step1 := split_union_polygon(inflate)
			{
				attribute<rdc_mm>        deflate (poly)                     := bg_buffer_polygon(geometry,  -parameters/big_inflate, 8b);  
			}
			
			unit<uint32> Step2 := split_union_polygon(Step1/deflate)
			{
				attribute<rdc_mm>        inflate (poly)                     := bg_buffer_polygon(geometry,  parameters/big_inflate, 8b);   
			}
		}
		
		unit<uint32> intersect  := ='overlay_polygon(Sloop_domain/Step2/deflate, NieuwbouwPerceel_Tile/Step2/inflate)' //intersect perceel met nieuwbouw met gesloopte panden
		{
			unit<uint32> nieuwbouw_perceel_metSloop := select_with_org_rel(IsDefined(second_rel)) //nieuwbouw perceel dat een gesloopt pand bevat
			{
				attribute<uint32>        second_rel                         := org_rel -> second_rel;
				attribute<rdc_mm>        geometry_mm (poly)                 := NieuwbouwPerceel_Tile/Step2/inflate[second_rel];
				
				unit<uint32> overlay_metSloopPercelen  := ='overlay_polygon(SloopPerceel_Tile/Step2/inflate, geometry_mm)'; //intersect nieuwbouw_perceel_metSloop met gesloopte panden
			}
			
			unit<uint32> TouchingSloopPercelen := select_with_org_rel(IsDefined(nieuwbouw_perceel_metSloop/overlay_metSloopPercelen/first_rel))
			{
				attribute<uint32>        first_rel                          := org_rel -> first_rel;
				attribute<rdc_mm>        geometry_mm (poly)                 := SloopPerceel_Tile/Step2/inflate[first_rel];
			}
		}
		
		unit<uint32> NB_metSloop_TouchSloopPerc := union_unit(intersect/nieuwbouw_perceel_metSloop, intersect/TouchingSloopPercelen)
		{
			attribute<rdc_mm>            geometry_mm (poly)                 := union_data(., intersect/nieuwbouw_perceel_metSloop/geometry_mm, intersect/TouchingSloopPercelen/geometry_mm);
			
			unit<uint32> Step1 := split_union_polygon(geometry_mm)
			{
				attribute<rdc_mm>        deflate (poly)                     := bg_buffer_polygon(geometry,  -parameters/small_inflate, 8b);
			}
			
			unit<uint32> Step2 := split_union_polygon(Step1/deflate)
			{
				attribute<rdc_mm>        inflate (poly)                     := bg_buffer_polygon(geometry,  parameters/big_inflate, 8b);
			}
			
			unit<uint32> Step3 := split_union_polygon(Step2/inflate)
			, StorageName = "%LocalDataProjDir%/Temp/SN.fss"
			{
				attribute<rdc_mm>        geometry (poly)                    : DisableStorage = "true";
				attribute<rdc>           geometry_rd (poly)                 := geometry[rdc];
				attribute<rdc_mm>        inflate (poly)                     := bg_buffer_polygon(geometry,  parameters/big_inflate, 8b), DisableStorage = "true";
			}
			
			unit<uint32> Read_Step3 
			: StorageName = "%LocalDataProjDir%/Temp/SN.fss"
			, StorageReadOnly = "true"
			, Dialogdata = "geometry_rd"
			, DialogType = "map"
			{
				attribute<rdc>           geometry_rd (poly);
				attribute<m2>            area                               := area(geometry_rd, m2);
				
				attribute<.> uq_N_pand_perceel_rel (uq_N_pand) := point_in_polygon(centroid_or_mid(uq_N_pand/pand_geometry), geometry_rd);
				attribute<.> uq_S_pand_perceel_rel (uq_S_pand) := point_in_polygon(centroid_or_mid(uq_S_pand/pand_geometry), geometry_rd);
				
				attribute<m2> uq_N_pand_area_op_perceel := sum(uq_N_pand/pand_area,uq_N_pand_perceel_rel);
				attribute<m2> uq_S_pand_area_op_perceel := sum(uq_S_pand/pand_area,uq_S_pand_perceel_rel);
				
				attribute<float32> share_N_pand_op_perceel := uq_N_pand_area_op_perceel / area;
				attribute<float32> share_S_pand_op_perceel := uq_S_pand_area_op_perceel / area;
				
				unit<uint32> uq_N_pand := unique(PrepDomains/n/domain/label)
				{
					attribute<rdc> pand_geometry (poly)                 := PrepDomains/n/domain/pand_geometry[invert(uq_N_pand_rel)];
					attribute<m2>  pand_area                            := area(pand_geometry, m2);
					attribute<.>   uq_N_pand_rel (PrepDomains/n/domain) := rlookup(PrepDomains/n/domain/label, values);
				}
				
				unit<uint32> uq_S_pand := unique(PrepDomains/S/domain/label)
				{
					attribute<rdc> pand_geometry (poly)                 := PrepDomains/S/domain/pand_geometry[invert(uq_S_pand_rel)];
					attribute<m2>  pand_area                            := area(pand_geometry, m2);
					attribute<.>   uq_S_pand_rel (PrepDomains/S/domain) := rlookup(PrepDomains/S/domain/label, values);
				}
			}
			
			unit<uint32> CleanUp := select_with_org_rel(Read_Step3/area >= Parameters/minimum_site_area && Read_Step3/share_N_pand_op_perceel > Parameters/minimum_SN_area_share && Read_Step3/share_S_pand_op_perceel > Parameters/minimum_SN_area_share)
			// unit<uint32> CleanUp := select_with_org_rel(Step3/area >= Parameters/minimum_site_area && Step3/share_N_pand_op_perceel > Parameters/minimum_SN_area_share && Step3/share_S_pand_op_perceel > Parameters/minimum_SN_area_share)
			{
				attribute<upoint>        fdom_id                            := point(const(0,.), id(.));
				attribute<rdc>           geometry  (poly)                   := org_rel -> geometry_rd;
				attribute<rdc_mm>        geometry_mm (poly)                 := geometry[rdc_mm];

				attribute<m2>            area                               := area(geometry, m2);
				attribute<jaar>          Avg_bouwjaar                       := mean(Sloop/pand_bouwjaar, Sloop_rel);
				attribute<jaar>          SD_bouwjaar                        := sd(Sloop/pand_bouwjaar, Sloop_rel);
				attribute<int32>         First_Nieuwbouw_MutatieMaand       := min(IsDefined(Nieuwbouw/MutatieMaand) ? Nieuwbouw/MutatieMaand : 99999999i, Nieuwbouw_rel);
				attribute<int32>         First_Sloop_MutatieMaand           := min(IsDefined(Sloop/MutatieMaand)     ? Sloop/MutatieMaand     : 99999999i, Sloop_rel);
				attribute<int32>         Last_Nieuwbouw_MutatieMaand        := max(IsDefined(Nieuwbouw/MutatieMaand) ? Nieuwbouw/MutatieMaand : 9999i, Nieuwbouw_rel);
				attribute<int32>         Last_Sloop_MutatieMaand            := max(IsDefined(Sloop/MutatieMaand)     ? Sloop/MutatieMaand     : 9999i, Sloop_rel);
				
				attribute<int32>         MutatieMaand                       := First_Sloop_MutatieMaand > 21000000i 
																					? Last_Nieuwbouw_MutatieMaand < 0i 
																						? null_i 
																						: Last_Nieuwbouw_MutatieMaand
																					: First_Sloop_MutatieMaand;
				
				unit<uint32>             Sloop                              := PrepDomains/S/domain;
				unit<uint32>             Nieuwbouw                          := PrepDomains/N/domain;
				attribute<string>        Site_ID                            := 'SN_'+string(id(.));
				attribute<.>             Sloop_rel     (Sloop)              := point_in_polygon(Sloop/vbo_geometry, geometry);
				attribute<.>             Nieuwbouw_rel (Nieuwbouw)          := point_in_polygon(Nieuwbouw/vbo_geometry, geometry);
				
				unit<uint32> Sloop_Objecten     := select_with_org_rel(IsDefined(Sloop_rel))
				{
					attribute<rdc>    pand_geometry (poly) := Sloop/pand_geometry[org_rel];
					attribute<rdc>    vbo_geometry         := Sloop/vbo_geometry[org_rel];
					attribute<int32>  MutatieMaand         := Sloop/MutatieMaand[org_rel];
					attribute<int32>  vbo_oppervlakte      := Sloop/vbo_oppervlakte[org_rel];
					attribute<jaar>   pand_bouwjaar        := Sloop/pand_bouwjaar[org_rel];
					attribute<string> Site_ID              := ../Site_ID[../sloop_rel][org_rel];
				}
				unit<uint32> Nieuwbouw_Objecten := select_with_org_rel(IsDefined(Nieuwbouw_rel))
				{
					attribute<rdc>    pand_geometry (poly) := Nieuwbouw/pand_geometry[org_rel];
					attribute<rdc>    vbo_geometry         := Nieuwbouw/vbo_geometry[org_rel];
					attribute<int32>  MutatieMaand         := Nieuwbouw/MutatieMaand[org_rel];
					attribute<int32>  vbo_oppervlakte      := Nieuwbouw/vbo_oppervlakte[org_rel];
					attribute<jaar>   pand_bouwjaar        := Nieuwbouw/pand_bouwjaar[org_rel];
					attribute<string> Site_ID              := ../Site_ID[../Nieuwbouw_rel][org_rel];
				}
			}
		}
		
		unit<uint32> Result := NB_metSloop_TouchSloopPerc/CleanUp;
	}
	
	unit<uint32> TeClusterenTypes : nrofrows = 4
	{
		attribute<string> name : ['T','O','TMplus', 'Onveranderd'];
	}

	container Clustering :=
		for_each_ne(
			TeClusterenTypes/name
			, 'Clustering_T('+quote(TeClusterenTypes/name)+')'
		);
	
	Template Clustering_T
	{
		parameter<string> RedevType;
		///
		unit<uint32> domain := ='PrepDomains/'+RedevType+'/domain';
		
		unit<uint32> Step1 := split_union_polygon(domain/pand_geometry[rdc_mm])
		{
			attribute<rdc_mm> inflate (poly) := bg_buffer_polygon(geometry, parameters/small_inflate, 8b);
		}
		
		unit<uint32> Step2 := split_union_polygon(Step1/inflate)
		{
			attribute<rdc_mm> deflate     (poly) := bg_buffer_polygon(geometry, -parameters/small_inflate, 8b); //, DisableStorage = "true";
			attribute<rdc>    geometry_rd (poly) := deflate[rdc];
			attribute<m2>     area               := area(geometry_rd, m2);
		}
		
		unit<uint32> CleanUp := select_with_org_rel(Step2/area >= Parameters/minimum_site_area)
		{
			attribute<rdc>               geometry (poly)                    := org_rel -> Step2/geometry_rd;
			attribute<string>            Site_ID                            := RedevType+'_'+string(id(.));
			attribute<jaar>              Avg_bouwjaar                       := mean(domain/pand_bouwjaar, Objecten_rel);
			attribute<jaar>              SD_bouwjaar                        := sd(domain/pand_bouwjaar, Objecten_rel);
			attribute<int32>             MutatieMaand                       := min(domain/MutatieMaand, Objecten_rel) > 21000000i || min(domain/MutatieMaand, Objecten_rel) < 0i
																				? null_i 
																				: min(domain/MutatieMaand, Objecten_rel);
			attribute<m2>                area                               := area(geometry, m2);
			
			attribute<.>                 Objecten_rel     (domain)          := point_in_polygon(domain/vbo_geometry, geometry);
			
			unit<uint32>                 Objecten                           := select_with_org_rel(IsDefined(Objecten_rel))
			{
				attribute<rdc>    pand_geometry (poly) := domain/pand_geometry[org_rel];
				attribute<rdc>    vbo_geometry         := domain/vbo_geometry[org_rel];
				attribute<int32>  MutatieMaand         := domain/MutatieMaand[org_rel];
				attribute<int32>  vbo_oppervlakte      := domain/vbo_oppervlakte[org_rel];
				attribute<jaar>   pand_bouwjaar        := domain/pand_bouwjaar[org_rel];
				attribute<string> Site_ID              := RedevType+'_'+string(Objecten_rel[org_rel]);
			}
		}
	}
}
